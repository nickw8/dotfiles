# ---- Profiling: Start -----
# zmodload zsh/zprof

# zmodload zsh/datetime
# setopt PROMPT_SUBST
# PS4='+$EPOCHREALTIME %N:%i> '

# logfile=$(mktemp zsh_profile.XXXXXXXX)
# echo "Logging to $logfile"
# exec 3>&2 2>$logfile

# setopt XTRACE

# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
# POWERLEVEL9K_DISABLE_CONFIGURATION_WIZARD=true

# ====================================
# ✨ Zinit Initialization
# ====================================
typeset -gAH ZINIT

ZINIT=(
  [HOME_DIR]=$HOME/.local/share/zsh/zinit
  [BIN_DIR]=$HOME/.local/share/zsh/zinit/zinit.git
  [OPTIMIZE_OUT_DISK_ACCESSES]=1
  [COMPLETIONS_DIR]=$HOME/.local/share/zsh/zinit/completions
  [SNIPPETS_DIR]=$HOME/.local/share/zsh/zinit/snippets
  [ZCOMPDUMP_PATH]=$HOME/.local/share/zsh/zinit/zcompdump
  [PLUGINS_DIR]=$HOME/.local/share/zsh/zinit/plugins
)

ZPFX=$ZINIT[HOME_DIR]/polaris
ZI_REPO='zdharma-continuum'
GH_RAW_URL='https://raw.githubusercontent.com'

if [[ ! -e $ZINIT[BIN_DIR] ]]; then
  info 'Downloading Zinit'
  command mkdir -p ${ZINIT[HOME_DIR]}
  command git clone https://github.com/${ZI_REPO}/zinit.git ${ZINIT[BIN_DIR]} || {
    error 'Failed to clone Zinit repository'
    return 1
  }
  info 'Setting up Zinit'
  command chmod g-rwX ${ZINIT[HOME_DIR]}
  zcompile ${ZINIT[BIN_DIR]}/zinit.zsh
  info 'Successfully installed Zinit'
fi

if [[ -e "${ZINIT[BIN_DIR]}/zinit.zsh" ]]; then
  source "${ZINIT[BIN_DIR]}/zinit.zsh"
  autoload -Uz _zinit
  (( ${+_comps} )) && _comps[zinit]=_zinit
else
  error "Unable to find 'zinit.zsh'"
  return 1
fi

# ====================================
# ✨ Prompt Configuration
# ====================================

POWERLEVEL9K_DISABLE_CONFIGURATION_WIZARD=true

if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

zinit ice depth=1; zinit light romkatv/powerlevel10k
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh

# ====================================
# ✨ Zinit: Oh-My-Zsh & Prezto Plugins
# ====================================

zinit wait lucid is-snippet for \
  OMZL::{'clipboard','compfix','completion','git','grep'}.zsh \
  OMZP::{git,sudo,aws,kubectl,kubectx,rust,command-not-found,eza}

# ====================================
# ✨ Zinit: ANNEXES 
# ====================================

# zinit lucid light-mode id-as for \
#   "$ZI_REPO"/zinit-annex-binary-symlink
#   "$ZI_REPO"/zinit-annex-{'bin-gem-node','binary-symlink','patch-dl','submods'}

# ====================================
# ✨ Zinit: Programs
# ====================================

export CARAPACE_BRIDGES='zsh,fish,bash,inshellisense'

# ====================================
# ✨ Zinit: Plugins
# ====================================

zinit wait lucid light-mode id-as for \
  Aloxaf/fzf-tab \
    atload'_zsh_autosuggest_start' \
    atinit"
      ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE=50
      bindkey '^_' autosuggest-execute
      bindkey '^ ' autosuggest-accept" \
  zsh-users/zsh-autosuggestions \
    atinit"ZINIT[COMPINIT_OPTS]=-C; zicompinit; zicdreplay" \
  $ZI_REPO/fast-syntax-highlighting 

# ====================================
# ✨ OS-specific Settings
# ====================================

if [[ "$(uname)" == "Darwin" ]]; then
  zi add-fpath /opt/homebrew/share/zsh/site-functions
fi

zi add-fpath $XDG_DATA_HOME/zsh/site-functions

# ====================================
# ✨ ZINIT: Source Local Configs
# ====================================

setopt EXTENDED_GLOB

vendor_env_dir=$XDG_CONFIG_HOME/zsh/vendors
user_dir=$XDG_CONFIG_HOME/zsh/user/

zinit wait lucid is-snippet id-as'' for \
  "$vendor_env_dir/"mise.zsh \
  "$vendor_env_dir/"^mise.zsh \
  "$user_dir/"*.zsh

# ====================================
# ✨ Zinit: Finalize Setup
# ====================================

zinit for atload'
      zicompinit; zicdreplay
      _zsh_highlight_bind_widgets
      _zsh_autosuggest_bind_widgets' \
    as'null' id-as'zinit/cleanup' lucid nocd wait \
  $ZI_REPO/null

# Bug in zsh-autosuggestions. This fixes it
unset ZSH_AUTOSUGGEST_USE_ASYNC

# ---- Profiling: End -----
# unsetopt XTRACE
# exec 2>&3 3>&-
