# ---- Profiling: Start -----
# zmodload zsh/zprof

# zmodload zsh/datetime
# setopt PROMPT_SUBST
# PS4='+$EPOCHREALTIME %N:%i> '

# logfile=$(mktemp zsh_profile.XXXXXXXX)
# echo "Logging to $logfile"
# exec 3>&2 2>$logfile

# setopt XTRACE

# ---- Helper Methods -----
function error() { print -P "%F{red}[ERROR]%f: %F{yellow}$1%f" && return 1 }
function info() { print -P "%F{blue}[INFO]%f: %F{cyan}$1%f" }

# ---- Zinit Initialization -----
typeset -gAH ZINIT

ZINIT=(
  [HOME_DIR]=$HOME/.local/share/zsh/zinit
  [BIN_DIR]=$HOME/.local/share/zsh/zinit/zinit.git
  [OPTIMIZE_OUT_DISK_ACCESSES]=1
  [COMPLETIONS_DIR]=$HOME/.local/share/zsh/zinit/completions
  [SNIPPETS_DIR]=$HOME/.local/share/zsh/zinit/snippets
  [ZCOMPDUMP_PATH]=$HOME/.local/share/zsh/zinit/zcompdump
  [PLUGINS_DIR]=$HOME/.local/share/zsh/zinit/plugins
)

ZPFX=$ZINIT[HOME_DIR]/polaris
ZI_REPO='zdharma-continuum'
GH_RAW_URL='https://raw.githubusercontent.com'

if [[ ! -e $ZINIT[BIN_DIR] ]]; then
  info 'Downloading Zinit'
  command mkdir -p ${ZINIT[HOME_DIR]}
  command git clone https://github.com/${ZI_REPO}/zinit.git ${ZINIT[BIN_DIR]} || {
    error 'Failed to clone Zinit repository'
    return 1
  }
  info 'Setting up Zinit'
  command chmod g-rwX ${ZINIT[HOME_DIR]}
  zcompile ${ZINIT[BIN_DIR]}/zinit.zsh
  info 'Successfully installed Zinit'
fi

if [[ -e "${ZINIT[BIN_DIR]}/zinit.zsh" ]]; then
  source "${ZINIT[BIN_DIR]}/zinit.zsh"
  autoload -Uz _zinit
  (( ${+_comps} )) && _comps[zinit]=_zinit
else
  error "Unable to find 'zinit.zsh'"
  return 1
fi

# ---- Zinit: Oh-My-Zsh & Prezto Plugins -----
zinit wait lucid for is-snippet \
  OMZL::{'clipboard','compfix','completion','git','grep'}.zsh \
  OMZP::{git,sudo,aws,kubectl,kubectx,rust,command-not-found,eza}

# ---- Zinit: ANNEXES -----
zi lucid light-mode for \
  "$ZI_REPO"/zinit-annex-binary-symlink
#   "$ZI_REPO"/zinit-annex-{'bin-gem-node','binary-symlink','patch-dl','submods'}

# ---- Zinit: GH Binaries -----
zi wait lucid from'gh-r' lbin'!' for \
  @dandavison/delta  \
  @koalaman/shellcheck \
    atclone"./fzf --zsh > _fzf.zsh" \
    atpull"%atclone" src"_fzf.zsh" \
  @junegunn/fzf \
    nocompletions'!' \
  @sharkdp/fd \
    lbin'!* -> shfmt' \
  @mvdan/sh \
    lbin'!**/rg' nocompletions \
  @BurntSushi/ripgrep \
    lbin \
  @eza-community/eza

# ---- Zinit: Programs -----
zi wait lucid light-mode from'gh-r' lbin'!' as'program' for \
    atclone'./direnv hook zsh > zhook.zsh' atpull'%atclone' \
    src="zhook.zsh" \
  direnv/direnv \
  tofuutils/tenv \
    id-as'carapace' atclone'carapace _carapace > carapace.zsh' \
    atpull'%atclone' src="carapace.zsh" \
  carapace-sh/carapace-bin

export CARAPACE_BRIDGES='zsh,fish,bash,inshellisense'

# ---- Zinit: Commands -----
zi wait lucid light-mode from"gh-r" lbin"!" as"command" for \
    atclone"./zoxide init --cmd cd zsh > init.zsh" \
    atpull"%atclone" src"init.zsh" \
  ajeetdsouza/zoxide \
    bpick"atuin-*.tar.gz" mv"atuin*/atuin -> atuin" \
    atclone"./atuin init zsh > init.zsh" \
    atpull"%atclone" src"init.zsh" nocompletions \
  atuinsh/atuin

# ---- Zinit: Prompt -----
# Uncomment below on first run
# zinit ice wait'!' lucid atload'true; _p9k_precmd' nocd
# Uncomment below on subsequent runs
zinit ice wait'!' lucid atload'source $HOME/.p10k.zsh; _p9k_precmd' nocd
zinit light romkatv/powerlevel10k

# ---- Zinit: Misc. -----
zinit wait lucid light-mode for \
  Aloxaf/fzf-tab \
    atload'_zsh_autosuggest_start' \
    atinit"
      ZSH_AUTOSUGGEST_BUFFER_MAX_SIZE=50
      bindkey '^_' autosuggest-execute
      bindkey '^ ' autosuggest-accept" \
  zsh-users/zsh-autosuggestions \
    atclone'(){local f;cd -q â†’*;for f (*~*.zwc){zcompile -Uz -- ${f}};}' \
    atload'FAST_HIGHLIGHT[chroma-man]=' atpull'%atclone' \
    compile'.*fast*~*.zwc' nocompletions \
  $ZI_REPO/fast-syntax-highlighting

zinit wait'1' lucid is-snippet for \
    $XDG_CONFIG_HOME/zsh/atuin-history-arrow.zsh \
    $XDG_CONFIG_HOME/zsh/aliases.zsh \
    $XDG_CONFIG_HOME/zsh/functions.zsh \
    $XDG_CONFIG_HOME/fzf/fzf-conf.zsh
    # $XDG_CONFIG_HOME/zsh/zstyle-completions.zsh

zinit for atload'
      zicompinit; zicdreplay
      _zsh_highlight_bind_widgets
      _zsh_autosuggest_bind_widgets' \
    as'null' id-as'zinit/cleanup' lucid nocd wait \
  $ZI_REPO/null

# ---- Recompile .zshrc if updated -----
if [[ ~/.zshrc -nt ~/.zshrc.zwc ]]; then
    zcompile ~/.zshrc
fi

# ---- Profiling: End -----
# unsetopt XTRACE
# exec 2>&3 3>&-
